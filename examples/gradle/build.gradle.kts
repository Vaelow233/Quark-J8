plugins {
    id("java-library")
    id("com.gradleup.shadow") version "8.3.11"
    id("org.vaelow233.quark") // version "1.x.x" // <-- uncomment in your project and set the version
}

repositories {
    mavenCentral()
    maven("https://hub.spigotmc.org/nexus/content/groups/public/")
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT")
    quark("com.google.code.gson:gson:2.10.1") // example of a dependency that will be downloaded by quark
}

quark {
    platform = "bukkit" // Specify the platform that will be used in your project (bukkit, paper, velocity or bungee)

    repositories {
        includeProjectRepositories()
    }
}

val pluginName = "QuarkGradleExamplePlugin"

tasks {
    build {
        dependsOn(shadowJar)
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(8))
    }

    jar {
        enabled = false
    }

    shadowJar {
        archiveBaseName.set(pluginName)
        archiveClassifier.set("")
        minimize()
    }
}

// don't use the following section in your build file
configurations.configureEach {
    resolutionStrategy.dependencySubstitution {
        substitute(module("org.vaelow233.quark:bukkit"))
            .using(project(":quark-bukkit"))
    }
}
